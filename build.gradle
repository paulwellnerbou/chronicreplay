plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'maven-publish'

group = 'de.wellnerbou'
version = '1.4'

description = """chronicreplay"""

mainClassName = "de.wellnerbou.chronic.replay.ChronicReplay"

// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}
//if (!project.hasProperty("publishPassword")) {
//    ext.publishPassword = ""
//}

sourceCompatibility = 1.7
targetCompatibility = 1.7

run {
	if ( project.hasProperty('args') ) {
		args project.args.split('\\s+')
	}
}

test {
	filter { includeTestsMatching "*Test" }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'chronicreplay'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = "$publishUser"
    key = "$publishKey"
    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'chronicreplay'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/paulwellnerbou/chronicreplay'
    }
}

repositories { maven { url "http://repo.maven.apache.org/maven2" } }

dependencies {
	compile group: 'com.ning', name: 'async-http-client', version:'1.7.22'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
    compile 'com.lexicalscope.jewelcli:jewelcli:0.8.9'
	compile group: 'joda-time', name: 'joda-time', version:'2.3'
	compile group: 'com.google.guava', name: 'guava', version:'15.0'
    compile group: 'org.glassfish.grizzly', name: 'grizzly-http', version:'2.3.11'
    compile group: 'org.glassfish.grizzly', name: 'grizzly-websockets', version:'2.3.11'
	testCompile group: 'junit', name: 'junit', version:'4.+'
	testCompile group: 'org.easytesting', name: 'fest-assert-core', version:'2.0M10'
}
